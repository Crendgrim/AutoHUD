plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'com.modrinth.minotaur' version '2.+'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

def extractedDir = "$buildDir/dependencies/extractedDir"

repositories {
	maven { url "https://jitpack.io" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven {
		name 'Xander Maven'
		url 'https://maven.isxander.dev/releases'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}
	flatDir { dir "${rootProject.getProjectDir()}/lib" }
}

configurations {
	jijArchive
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"

	implementation include("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")
	annotationProcessor("com.github.LlamaLad7:MixinExtras:${project.mixin_extras_version}")

	modImplementation(fabricApi.module("fabric-api-base", project.fabric_api_version))
	modImplementation(fabricApi.module("fabric-convention-tags-v1", project.fabric_api_version))
	modImplementation(fabricApi.module("fabric-client-tags-api-v1", project.fabric_api_version))
	modImplementation(fabricApi.module("fabric-lifecycle-events-v1", project.fabric_api_version))
	modImplementation(fabricApi.module("fabric-key-binding-api-v1", project.fabric_api_version))
	modImplementation(fabricApi.module("fabric-rendering-v1", project.fabric_api_version))

	modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"
	modImplementation include(name: "yaclx", group: "mod.crend.yaclx", version: "${project.yaclx_version}-fabric")
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	modCompileOnly("maven.modrinth:appleskin:${project.appleskin_version}") { transitive = false }
	modCompileOnly("maven.modrinth:detail-armor-bar:${project.detailab_version}") { transitive = false }
	modCompileOnly("curse.maven:dehydration-${project.dehydration_version}") { transitive = false }
	modCompileOnly("maven.modrinth:inventory-profiles-next:${project.inventory_profiles_next_version}") { transitive = false }
	modCompileOnly("maven.modrinth:onebar:${project.onebar_version}") { transitive = false }
	modCompileOnly("curse.maven:status-effect-bars-${project.status_effect_bars_version}") { transitive = false }
	jijArchive "maven.modrinth:microdurability:${project.microdurability_version}"
	modCompileOnly fileTree(dir: "${extractedDir}", include: '*.jar')
}

// Special magic to extract a jar-in-jar archive and add it to the include path
task extractJij(type: Copy) {
	configurations.jijArchive.asFileTree.each {
		zipTree(it).each { it2 ->
			if (it2.name.endsWith(".jar")) {
				from(it2)
				compileJava.classpath += files("${extractedDir}/${it2.getName()}")
			}
		}
	}
	into extractedDir
}
compileJava.dependsOn extractJij

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") {
		expand(project.properties)
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = project.modrinth_id
	uploadFile = remapJar
	versionType = project.version_type
	changelog = project.changelog
	gameVersions = List.of(project.supported_versions.split(';'))
	loaders = ['fabric']
	dependencies {
		required.project 'P7dR8mSH' // Fabric API
		optional.project '1eAoo2KR' // YetAnotherConfigLib
		optional.project 'mOgUt4GM' // Mod Menu
	}
}

curseforge {
	apiKey = "${System.getenv("CURSEFORGE_TOKEN")}"
	project {
		id = project.curseforge_id
		changelog = project.changelog
		releaseType = project.version_type
		addGameVersion "Fabric"
		project.supported_versions.split(';').each {
			addGameVersion it
		}

		mainArtifact(remapJar) {
			displayName = "${project.mod_name} ${project.version}"
			relations {
				requiredDependency "fabric-api"
				optionalDependency "yacl"
				optionalDependency "modmenu"
			}
		}
	}
	options {
		forgeGradleIntegration = false
	}
}
